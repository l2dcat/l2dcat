function(bongo_cat_neo_configure_embedded_cubism_assets)
  set(BONGO_CAT_NEO_ASSET_EXTRA_COMMANDS PARENT_SCOPE)
  if(NOT BONGO_CAT_NEO_CUBISM_ENABLED)
    return()
  endif()
  file(GLOB_RECURSE shader_inputs CONFIGURE_DEPENDS
    "${CUBISM_FRAMEWORK_PATH}/src/Rendering/OpenGL/Shaders/Standard/*")
  set(BONGO_CAT_NEO_ASSET_INPUTS ${BONGO_CAT_NEO_ASSET_INPUTS} ${shader_inputs} PARENT_SCOPE)
  set(BONGO_CAT_NEO_ASSET_EXTRA_COMMANDS
    COMMAND ${CMAKE_COMMAND} -E copy_directory
      "${CUBISM_FRAMEWORK_PATH}/src/Rendering/OpenGL/Shaders/Standard"
      "${BONGO_CAT_NEO_ASSET_STAGE}/assets/FrameworkShaders" PARENT_SCOPE)
endfunction()

function(bongo_cat_neo_stage_cubism_assets target)
  if(NOT BONGO_CAT_NEO_CUBISM_ENABLED)
    return()
  endif()
  add_custom_command(TARGET ${target} POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_directory
      "${BONGO_CAT_NEO_CUBISM_SDK}/Framework/src/Rendering/OpenGL/Shaders/Standard"
      "$<TARGET_FILE_DIR:${target}>/FrameworkShaders")
  if(UNIX AND NOT APPLE)
    install(DIRECTORY
      "${CUBISM_FRAMEWORK_PATH}/src/Rendering/OpenGL/Shaders/Standard/"
      DESTINATION assets/FrameworkShaders)
  endif()
endfunction()
